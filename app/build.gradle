apply plugin: 'com.android.application'

repositories {
    maven {
        url 'https://download.01.org/crosswalk/releases/crosswalk/android/maven2'
    }
}

Properties properties = new Properties();
properties.load(project.rootProject.file('local.properties').newDataInputStream());

def RELEASE_STORE_FILE = properties.getProperty('RELEASE_STORE_FILE');
def RELEASE_STORE_PASSWORD = properties.getProperty('RELEASE_STORE_PASSWORD');
def RELEASE_KEY_ALIAS = properties.getProperty('RELEASE_KEY_ALIAS');
def RELEASE_KEY_PASSWORD = properties.getProperty('RELEASE_KEY_PASSWORD');

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"
    defaultConfig {
        applicationId "com.bkfinds.browser3"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 102
        versionName "1.0.102"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    splits {
        abi {
            enable true
            reset()
            include "x86", "armeabi-v7a"
        }
    }
}

ext.abiCodes = ['armeabi-v7a': 1, x86: 2]

import com.android.build.OutputFile

android.applicationVariants.all { variant ->
    variant.outputs.each { output ->
        def baseAbiVersionCode = project.ext.abiCodes.get(output.getFilter(OutputFile.ABI));

        if (baseAbiVersionCode != null) {
            output.versionCodeOverride = baseAbiVersionCode * 10000 + variant.versionCode
        }

    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.journeyapps:zxing-android-embedded:3.5.0'
    compile 'org.xwalk:xwalk_core_library:23.53.589.4'
    testCompile 'junit:junit:4.12'
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion '25.3.1'
            }
        }
    }
}